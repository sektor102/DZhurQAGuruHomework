import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
}

wrapper {
    gradleVersion = '8.12'
}

repositories {
    mavenCentral()
}

group = 'org.example'
version = '1.0-SNAPSHOT'

def allureVersion = '2.29.0'
def aspectJVersion = '1.9.22'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    agent "org.aspectj:aspectjweaver:$aspectJVersion"

    testImplementation(
            'org.junit.jupiter:junit-jupiter:5.11.0',
            'com.codeborne:selenide:7.9.4',
            'commons-io:commons-io:2.20.0',
            'com.codeborne:pdf-test:2.0.0',
            'com.codeborne:xls-test:1.7.2',
            'org.apache.poi:poi:5.2.5',
            'org.apache.poi:poi-ooxml:5.4.1',
            'com.opencsv:opencsv:5.12.0',
            'com.fasterxml.jackson.core:jackson-databind:2.20.0',
            'io.rest-assured:rest-assured:5.5.0',
            'io.rest-assured:json-schema-validator:5.5.0',
            "io.qameta.allure:allure-junit5:$allureVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            'org.projectlombok:lombok:1.18.42'
    )

    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine',
            'org.slf4j:slf4j-simple:2.0.17'
    )

    implementation(
            'net.datafaker:datafaker:2.4.4',
            'com.fasterxml.jackson.core:jackson-core:2.20.0',
            'com.fasterxml.jackson.core:jackson-databind:2.20.0',
            'org.projectlombok:lombok:1.18.42'
    )

    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
}

test {
    useJUnitPlatform {
        def tags = System.getProperty("tags")
        if (tags != null && !tags.isBlank()) {
            includeTags(tags)
        }

        def testClass = System.getProperty("testClass")
        if (testClass != null && !testClass.isBlank()) {
            includePackagesOrClasses(testClass)
        }
    }

    ignoreFailures = true
    jvmArgs = ["-javaagent:${configurations.agent.singleFile}"]

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

def ts = new SimpleDateFormat("yyyy-MM-dd_HH-mm-ss").format(new Date())

tasks.register("allureReportTs") {
    dependsOn(tasks.named("allureReport"))
    doLast {
        copy {
            from(layout.buildDirectory.dir("reports/allure-report"))
            into(layout.buildDirectory.dir("reports/allure-report-${ts}"))
        }
        println("Saved: ${layout.buildDirectory.get()}/reports/allure-report-${ts}")
    }
}
